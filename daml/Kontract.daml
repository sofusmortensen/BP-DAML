module Kontract where

import DA.Optional
import DA.Either
import DA.Time
import DA.Date
import DA.List
import DA.Numeric

import Effect

type KontractId = ContractId Kontract

createPayEffect : Service -> Party -> Party -> Text -> Decimal -> Optional Time -> Update TransferId
createPayEffect service payer payee asset amount (Some maturity) = do
  time <- getTime
  if time >= maturity
    then create Transfer with maturity = None; ..
    else create Transfer with maturity = Some maturity; ..
createPayEffect service payer payee asset amount None = do
  create Transfer with maturity = None; ..

template MultiSign
  with
    signed : [Party]
    toSign : [Party]
    contract : Kontract
  where
    signatory signed
    observer toSign    
  
    choice MultiSign_Sign : Either (ContractId MultiSign) KontractId
      with
        actor : Party
      controller actor
      do
        case toSign \\ [actor] of
          [] -> Right <$> create contract
          ps -> Left <$> create this with signed = actor :: signed; toSign = ps

epochDate = date 1970 Jan 1
epochTime = time epochDate 0 0 0

data Observable = Constant Decimal
                | FixingAbs Text
                | FixingOnDate Text
                | Plus with lhs : Observable; rhs : Observable
                | Mul with lhs : Observable; rhs : Observable
                | Div with lhs : Observable; rhs : Observable
                | Negate Observable
                | Sign Observable
                | Abs Observable
--                | Recip Observable
                | Max with lhs : Observable; rhs : Observable
                | Min with lhs : Observable; rhs : Observable
                | LessThan with lhs : Observable; rhs : Observable
                | Equal with lhs : Observable; rhs : Observable
                | After Observable
                | Var Int
                | Start
                | End
                | Dcf with startDate: Observable; endDate: Observable
                           dayCountConvention: Text
                deriving (Eq, Show)

fixing : Text -> Obs Decimal
fixing = Obs . FixingOnDate

fixingAbs : Text -> Obs Decimal
fixingAbs = Obs . FixingAbs

anytime : Obs Bool
anytime = lit True

never : Obs Bool
never = lit False

l : Decimal -> Obs Decimal
l = lit

ldate : Int -> Month -> Int -> Obs Date
ldate y m d = lit $ date y m d

class IsObservable a where
  lit : a -> Obs a
  castRaw : Decimal -> a

  fromObs : Obs a -> Optional a
  fromObs (Obs (Constant x)) = Some $ castRaw x
  fromObs _ = None

instance IsObservable Int where
  lit = Obs . Constant . intToDecimal
  castRaw = truncate

instance IsObservable Decimal where
  lit x = Obs $ Constant $ cast x
  castRaw = identity

instance IsObservable Bool where
  lit x = if x then Obs $ Constant 1.0 else Obs $ Constant 0.0
  castRaw = (>=0.5)

instance IsObservable Date where
  lit d = (Obs . Constant . intToDecimal) days
          where days = subDate d epochDate
  castRaw = addDays epochDate . truncate

instance IsObservable Time where
  lit d = (Obs . Constant . intToDecimal) msec
        where msec = convertRelTimeToMicroseconds $ subTime d epochTime
  castRaw = addRelTime epochTime . convertMicrosecondsToRelTime . truncate

instance IsObservable a => Additive (Obs a) where
  (Obs x) + (Obs y) = Obs $ Plus x y
  (Obs x) - (Obs y) = Obs $ Plus x (Negate y)
  negate (Obs x) = Obs $ Negate x
  aunit = Obs $ Constant 0.0

instance IsObservable a => Multiplicative (Obs a) where
  (Obs x) * (Obs y) = Obs $ Mul x y
  x ^ y = if y <= 0 then x else x * (x ^ (y - 1)) -- ignoring negatives...
  munit = Obs $ Constant 1.0

(|.) : Obs Bool -> Obs Bool -> Obs Bool
(Obs x) |. (Obs y) = Obs $ Max x y
(&.) : Obs Bool -> Obs Bool -> Obs Bool
(Obs x) &. (Obs y) = Obs $ Min x y

_not : Obs Bool -> Obs Bool
_not x = munit - x

dcf_ : Obs Date -> Obs Date -> Text -> Obs Decimal
dcf_ (Obs s) (Obs e) conv = Obs $ Dcf s e conv

dcf : Text -> Obs Decimal
dcf = dcf_ (Obs Start) (Obs End)

start : Obs Time
start = Obs Start

end : Obs Time
end = Obs End

instance IsObservable a => Divisible (Obs a) where
  (Obs x) / (Obs y) = Obs $ Div x y

data Actor = Holder | Writer
           deriving (Eq, Ord, Show)

data Actors = NoActor | OneActor Actor | Both
            deriving (Eq, Ord, Show)

swap : Actors -> Actors
swap (OneActor Holder) = OneActor Writer
swap (OneActor Writer) = OneActor Holder
swap x = x

member : Actor -> Actors -> Bool
member _ NoActor = False
member _ Both = True
member a1 (OneActor a2) = a1 == a2

union : Actors -> Actors -> Actors
union NoActor a = a
union a NoActor = a
union Both _ = Both
union _ Both = Both
union (OneActor a1) (OneActor a2) = if a1 == a2 then OneActor a1 else Both

data TransitionType = TransitionChoice (Actors, Text) 
                    | TransitionWhen 
                    deriving (Eq, Show)

data Transition = Transition
  with
    transitionType : TransitionType
    prerequisite : Obs Bool
    state : State
  deriving (Eq, Show)

applyFixingToObs : IsObservable a => Text -> Optional Date -> Decimal -> Obs a -> Obs a
applyFixingToObs name fixingdate value (Obs o) = Obs $ applyFixingToObs_ name fixingdate value o

applyFixingToObs_ : Text -> Optional Date -> Decimal -> Observable -> Observable
applyFixingToObs_ name fixingdate value o =
  case o of
    FixingAbs l -> case fixingdate of
      Some date -> FixingAbs l
      None -> if l == name then Constant value else FixingAbs l
    FixingOnDate l -> case fixingdate of
      Some date -> FixingAbs l
      None -> if l == name then Constant value else FixingAbs l
    Plus x y -> Plus (f x) (f y)
    Div x y -> Div (f x) (f y)
    Mul x y -> Mul (f x) (f y)
    Negate x -> Negate (f x)
    Sign x -> Sign (f x)
    Abs x -> Abs (f x)
    Max x y -> Max (f x) (f y)
    Min x y -> Min (f x) (f y)
    LessThan x y -> LessThan (f x) (f y)
    Equal x y -> Equal (f x) (f y)
    After x -> After (f x)
    _ -> o
  where
    f = applyFixingToObs_ name fixingdate value

reduce : Time -> Obs a -> Obs a
reduce now (Obs o) = Obs $ reduce_ now o

reduce_ : Time -> Observable -> Observable
reduce_ now = \case
    Constant v -> Constant v
    FixingAbs l -> FixingAbs l 
    --  optional (FixingOnDate l) (\x -> case M.lookup (l, x) fixings of None -> FixingOnDate l; Some v -> Constant v) fixingdate
    FixingOnDate l -> FixingOnDate l 
    --  optional (FixingOnDate l) (\x -> case M.lookup (l, x) fixings of None -> FixingOnDate l; Some v -> Constant v) fixingdate
    Start -> Start
    End -> End
    Var _ -> error "Var not implemented"
    Dcf {} -> error "Dcf not implemented"
    Plus x y -> fn2 (+) x y Plus
    Div x y -> fn2 (/) x y Div
    Mul x y -> fn2 (*) x y Mul
    Negate x -> fn1 negate x Negate
    Sign x -> fn1 signum x Sign
    Abs x -> fn1 abs x Abs
--    Recip x -> fn1 recip x Recip
    Max x y -> fn2 max x y Max
    Min x y -> fn2 min x y Min
    LessThan x y -> fn2 (\x' y' -> if x' < y' then 1.0 else 0.0) x y LessThan
    Equal x y -> fn2 (\x' y' -> if x' == y' then 1.0 else 0.0) x y Equal
    After maturity -> case reduce_ now maturity of
                        Constant m -> if now > (addRelTime epochTime (convertMicrosecondsToRelTime $ truncate m)) then Constant 1.0 else After $ Constant m
                        x' -> After x'
  where
    fn2 op x y c = case (reduce_ now x, reduce_ now y) of
                (Constant x', Constant y') -> Constant $ x' `op` y'
                (x', y') -> c x' y'
    fn1 op x c = case reduce_ now x of
                Constant x' -> Constant $ op x'
                x' -> c x'


class CanBeObserved a where
  eval : Observable -> Optional a

instance CanBeObserved Decimal where
  eval o = case o of
    Constant v -> Some v
    FixingOnDate _ -> None
    _ -> error "not implemented"

data Obs a = Obs Observable
           deriving (Eq, Show)

type Transitions = [Transition]

data Period = Days Int | Weeks Int | Months Int | Years Int
              deriving (Eq, Show)

data Offset = FromStart Period | FromEnd Period deriving (Eq, Show)

dateFromOffsetAndRange : Offset -> (Date, Date) -> Date
dateFromOffsetAndRange (FromStart p) (d, _) = dateShift p d
dateFromOffsetAndRange (FromEnd p) (_, d) = dateShift p d


dateShift : Period -> Date -> Date
dateShift (Days d) date = addDays date d
dateShift (Weeks w) date = addDays date $ w * 7
dateShift (Months dm) date =
  let (y, m, d) = toGregorian date
      m' = (fromEnum m) + dm
      ym = if m' >= 0 then (m' + 1) / 12 else (m' + 1) / 12 - 1
      y' = ym + y
      m'' = toEnum $ m' - 12 * ym 
      d' = min d (monthDayCount y' m'')
  in  fromGregorian (y', m'', d')
dateShift (Years ym) date =
  let (y, m, d) = toGregorian date
  in  fromGregorian (y + ym, m, d)

dateShift' : Period -> Date -> Int -> Date
dateShift' (Days d) date i = addDays date (i * d)
dateShift' (Weeks w) date i = addDays date $ (i * w * 7)
dateShift' (Months dm) date i =
  let (y, m, d) = toGregorian date
      m' = (fromEnum m) + i * dm
      ym = if m' >= 0 then (m' + 0) / 12 else (m' + 1) / 12 - 1
      y' = ym + y
      m'' = toEnum $ m' - 12 * ym 
      d' = min d (monthDayCount y' m'')
  in  fromGregorian (y', m'', d')
dateShift' (Years ym) date i =
  let (y, m, d) = toGregorian date
  in  fromGregorian (y + i * ym, m, d)

daysEnd : Int -> Offset
daysEnd = FromEnd . Days

daysStart : Int -> Offset
daysStart = FromStart . Days

data State = Void
             | And [State]
             | Give State
             | Pay with asset : Text; amount : Decimal; maturity : Optional Time
             | Scale with sc : Obs Decimal; state : State
             | Branch Transitions
             | Rollout with startDate : Obs Date
                            count : Int
                            index : Int
                            frequency : Period
                            fixingOffset : Offset
                            paymentOffset : Offset
                            usingContinuation : Bool
                            state : State
             | Continue
             deriving (Eq, Show)

class ObsCompare a b where
  (>.) : a -> b -> Obs Bool
  (<.) : a -> b -> Obs Bool
  (>=.) : a -> b -> Obs Bool
  (<=.) : a -> b -> Obs Bool
  (==.) : a -> b -> Obs Bool

  l >=. r = (l >. r) |. (l ==. r)
  l <=. r = (l <. r) |. (l ==. r)

instance ObsCompare (Obs a) (Obs b) where
  (Obs l) >. (Obs r) = Obs $ LessThan r l
  (Obs l) <. (Obs r) = Obs $ LessThan l r
  (Obs l) ==. (Obs r) = Obs $ Equal l r

instance IsObservable a => ObsCompare (Obs a) a where
  l >. r = l >. (lit r : Obs a)
  l <. r = l <. (lit r : Obs a)
  l ==. r = l ==. (lit r : Obs a)

instance IsObservable a => ObsCompare a (Obs a) where
  l >. r = (lit l : Obs a) >. r
  l <. r = (lit l : Obs a) <. r
  l ==. r = (lit l : Obs a) ==. r

class DateC a where
  after : a -> Obs Bool
-- after (Obs maturity) = Obs $ After maturity
  before : a -> Obs Bool
  before = _not . after

instance DateC (Obs Time) where
  after (Obs maturity) = Obs $ After maturity

instance DateC Time where
  after maturity = after (lit maturity)

scale_ : Obs Decimal -> State' b -> State' b
scale_ x (State' (v, s)) = State' (v, Scale x s)

payout_ : Text -> Obs Decimal -> State' ()
payout_ c a = State' ((), Scale a $ Pay c 1.0 None)


class Scalar a where
  scale : a -> State' b -> State' b
  payout : Text -> a -> State' ()

instance Scalar Decimal where
  scale x (State' (v, s)) = State' (v, Scale (lit x) s)
  payout asset amount = State' ((), Pay asset amount None) -- Scale (lit a) $ One c)

instance Scalar Int where
  scale x (State' (v, s)) = State' (v, Scale (lit $ intToDecimal x) s)
  payout asset amount = State' ((), Pay asset (intToDecimal amount) None) --Scale (lit $ intToDecimal a) $ One c)

instance Scalar (Obs Decimal) where
  scale x (State' (v, s)) = State' (v, Scale x s)
  payout asset amount = State' ((), Scale amount $ Pay asset 1.0 None) : State' ()


applyFixing : Text -> Optional Date -> Decimal -> State -> State
applyFixing name fixingdate value = \case
  Void -> Void
  Rollout { .. } -> Rollout { state = applyFixing name fixingdate value state; ..}
  Continue -> Continue
  And xs -> And $ applyFixing name fixingdate value <$> xs
  Pay { .. } -> Pay { .. }
  Give c -> Give $ applyFixing name fixingdate value c
  Scale o s -> Scale (applyFixingToObs name fixingdate value o) $ applyFixing name fixingdate value s
  Branch ts -> Branch $ go <$> ts
    where
      go tr = tr
        with 
          prerequisite = applyFixingToObs name fixingdate value tr.prerequisite
          state = applyFixing name fixingdate value tr.state

select : (Actor, Text) -> Bool -> State -> State
select (actor, name) rev = \case
  Void -> Void
  Rollout { .. } -> Rollout { state = select (actor, name) rev state;.. }
  Continue -> Continue
  And xs -> And $ select (actor, name) rev <$> xs
  Pay { .. } -> Pay { .. }
  Give c -> Give $ select (actor, name) (not rev) c
  Scale o s -> Scale o $ select (actor, name) rev s
  Branch ts -> Branch $ go <$> ts
    where
      go tr = case tr.transitionType of
                TransitionChoice tc -> 
                  if tc._2 == name && member actor tc._1
                    then tr with transitionType = TransitionWhen
                    else tr
                _ -> tr

data ReduceContext = ReduceContext
  with
    fixingDate : Date
    paymentDate : Time
    rolloutCont : State

applyContextToPayment : Optional ReduceContext -> Text -> Decimal -> Optional Time -> State
applyContextToPayment (Some ctx) asset amount (Some maturity)
  = Pay asset amount (Some maturity)
applyContextToPayment (Some ctx) asset amount None
  = Pay asset amount (Some ctx.paymentDate)

applyContextToPayment None asset amount maturity = Pay asset amount maturity


reduceState' : Time -> Decimal -> Bool -> Optional ReduceContext -> State -> [State]
reduceState' now sc rev ctx = if sc == 0.0 then const [Void] else \case
    Void -> []

    Rollout with startDate=Obs sd; .. ->
        case fromObs $ reduce now (Obs sd) of
          Some anchor -> 
              if usingContinuation
              then go index
              else concat $ go <$> [index .. (count - 1)]
            where 
              go i =
                let
                  startdate = dateShift' frequency anchor index
    --              lastdate = dateShift duration startdate
                  enddate = dateShift' frequency anchor (i + 1)
                  fixingDate = dateFromOffsetAndRange fixingOffset (startdate, enddate)
                  paymentDate =  cutLondon $ dateFromOffsetAndRange paymentOffset (startdate, enddate) -- todo, think about payment cut
                  rolloutCont = if usingContinuation && i + 1 < count then Rollout with startDate = Obs sd; index = i + 1; .. else Void
                in
                  reduceState' now sc rev (Some $ ReduceContext with ..) state
          _ -> [Rollout with startDate=Obs sd; .. ] -- startDate = sd; endDate = sd; ..]

    Continue -> case ctx of 
      Some ReduceContext with .. -> reduceState' now sc rev ctx rolloutCont
      None -> error "Continue used outside recursive Rollout"
    And xs -> concatMap (reduceState' now sc rev ctx) xs
    Pay asset amount maturity ->
        [if rev then Give p else p]
      where 
        p = applyContextToPayment ctx asset (sc * amount) maturity
    Give c -> reduceState' now sc (not rev) ctx c 
    Scale o s -> case reduce now o of
      Obs (Constant v) -> reduceState' now (sc * v) rev ctx s
      o' -> map (Scale o') (reduceState' now sc rev ctx s)
    Branch ts -> case res of
                  (x::_, _) -> x
                  ([], []) -> []
                  ([], ts) -> [Branch ts]
                where
                  res = fmap catOptionals $ partitionEithers $ map proc ts
                  proc : Transition -> Either [State] (Optional (Transition))
                  proc t =
                    case t.transitionType of
                      TransitionWhen ->
                        case fromObs $ reduce now t.prerequisite of
                          None -> Right $ Some t
                          Some False -> Right None
                          Some True -> Left $ reduceState' now sc rev ctx t.state
                      TransitionChoice tc ->
                        Right $ Some t

type Cid = Either KontractId TransferId
createFromState : Kontract -> State -> Update Cid
createFromState this (Pay asset amount maturity) = Right <$> createPayEffect this.service this.writer this.holder asset amount maturity
createFromState this (Give (Pay asset amount maturity)) = Right <$> createPayEffect this.service this.holder this.writer asset amount maturity
createFromState this state = Left <$> create this with state

template Kontract
  with
    state : State
    oracle : Optional Party
    holder : Party
    writer : Party
    service : Service
  where
    signatory holder, writer
    observer oracle

    choice Select : [Cid]
      with
        name : Text
        actor : Party
      controller actor
      do
        let ctrl
              | actor == holder = Holder
              | actor == writer = Writer
              | otherwise = error "Unauthorized"
        now <- getTime

        let s' = select (ctrl, name) False state
        assertMsg "no can do" $ s' /= state

        let s'' = reduceState' now 1.0 False None s'
        mapA (createFromState this) s''

    choice Lifecycle : [Cid]
      with
        actor : Party
      controller actor
      do
        now <- getTime
        mapA (createFromState this) $ reduceState' now 1.0 False None state


    choice Publish : [Cid]
      with name : Text
           fixingDate : Optional Date
           value : Decimal
      controller oracle
      do
        now <- getTime
        let state' = applyFixing name fixingDate value state
        mapA (createFromState this) $ reduceState' now 1.0 False None state'

cutLondon : Date -> Time
cutLondon d = time d 16 0 0  -- should be dealing with daylight saving

europeanOption : Obs Time -> State -> State
europeanOption maturity state =
           Branch [ Transition with transitionType = TransitionChoice (OneActor Holder, "exercise")
                                    prerequisite = before maturity
                                    state
                  , Transition with transitionType = TransitionChoice (Both, "expire")
                                    prerequisite = after maturity
                                    state = Void
                  ]

data State' a = State' (a, State) deriving (Show)

instance Functor State' where
  fmap = error "fmap @State' unexpected"

instance Applicative State' where
  pure x = error ""
  State' (l, sl) *> State' (r, sr) = State' (r, And [sl, sr])
  f <*> x = error "(<*>) unexpected"

instance Action State' where
  (State' (l, sl)) >>= f = 
    let State' (r, sr) = f l
    in State' (r, And [sl, sr])

data Kontract' a = Kontract' (a, [Transition]) deriving (Show)

instance Functor Kontract' where
  fmap = error "fmap @Kontract' unexpected"

instance Applicative Kontract' where
  pure x = error ""
  Kontract' (l, ol) *> Kontract' (r, or) = Kontract' (r, ol ++ or)
  f <*> x = error "(<*>) unexpected"

instance Action Kontract' where
  Kontract' (l, ol) >>= f = 
    let Kontract' (r, or) = f l
    in Kontract' (r, ol ++ or)

kontract : Kontract' a -> State' a
kontract (Kontract' (v, o)) = State' (v, Branch o)

when : Obs Bool -> State' a -> Kontract' a
when prerequisite (State' (v, state))
  = Kontract' (v, [Transition with state; prerequisite; transitionType = TransitionWhen ])

any_can : Obs Bool -> Text -> State' a -> Kontract' a
any_can prerequisite name (State' (v, state))
  = Kontract' (v, [Transition with transitionType = TransitionChoice (Both, name); ..])

holder_can : Obs Bool -> Text -> State' a -> Kontract' a
holder_can prerequisite name (State' (v, state))
  = Kontract' (v, [Transition with transitionType = TransitionChoice (OneActor Holder, name); ..])

writer_can : Obs Bool -> Text -> State' a -> Kontract' a
writer_can prerequisite name (State' (v, state))
  = Kontract' (v, [Transition with transitionType = TransitionChoice (OneActor Writer, name); ..])

give (State' (v, s)) = State' (v, Give s) 
void = State' ((), Void) 

rollout : Obs Date -> Period -> Int -> Offset -> Offset -> State' a -> State' ()
rollout sd freq cnt offsetFixing offsetPayment exp =
  let
    (State' (_, state)) = exp
  in
    State' ((), Rollout sd cnt 0 freq offsetFixing offsetPayment False state)

rolloutRecurse : Obs Date -> Period -> Int -> Offset -> Offset -> State' a -> State' ()
rolloutRecurse sd freq cnt offsetFixing offsetPayment exp =
  let
    (State' (_, state)) = exp
  in
    State' ((), Rollout sd cnt 0 freq offsetFixing offsetPayment True state)

continue : State' ()
continue = State' ((), Continue)

createAgreement : Party -> Party -> Optional Party -> Service -> State' a -> Kontract
createAgreement holder writer oracle service (State' (_, state)) = Kontract with ..

